buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
    }
    project {
        languageLevel = '1.8' //due to a bug in gradle or IDEA, this doesn't work :(
    }
}

version = "1.12-4.0.0-beta.4"
group = "de.sanandrew.mods"
archivesBaseName = "TurretMod"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20180301"
}

repositories {
    mavenCentral()
	maven {
		url = "http://dvs1.progwml6.com/files/maven"
	}
    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven {
        name = "SanLib"
        url = "https://github.com/SanAndreasP/SanLib/raw/1.12/maven/"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12.2:4.8.5.147:api"
    runtime "mezz.jei:jei_1.12.2:4.8.5.147"
    deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.19-11") { transitive = false }
    deobfCompile "de.sanandrew.mods:SanLib:1.12.2-1.6.0"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', '**/*.psd', '**/*.bbmodel'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    exclude "**/*.psd"
    manifest {
        attributes 'FMLAT': 'turretmodrebirth_at.cfg'
    }
}

sourceJar {
    from sourceSets.main.java
    appendix = 'src'
    archiveName = "${baseName}-${version}-${appendix}.jar"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
    archiveName = "${baseName}-${version}-${appendix}.jar"
    manifest {
        attributes 'FMLAT': 'turretmodrebirth_at.cfg'
    }
}

artifacts {
    archives deobfJar
    archives sourceJar
    archives jar
}
